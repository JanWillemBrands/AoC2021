whitespace  : /\s+/ .
singleLine  : /\/\/.*/ .
multiLine   : /\/\*(?s).*?\*\// .

identifier  = /\p{ID_Start}\p{ID_Continue}*/ .
literal     = /\"(?:[^\"\\]|\\.)*\"/ .
regex       = /\/(?:[^\/\\]|\\.)*\// .
action      = /@(?:[^@\\]|\\.)*@/ .
message     = /¶(?:[^¶\\]|\\.)*/ .

S     = { production } { message } .
              
production  = identifier ( ":" | "=" ) selection "." .
selection   = sequence { "|" sequence } .
sequence    = term { term } .
term        = terminal
            | "[" selection "]"
            | "{" selection "}"
            | "<" selection ">"
            | "(" selection ( ")"
                            | ")?"
                            | ")*"
                            | ")+"
                            ) .
terminal    = identifier | literal | regex | action .

¶
whitespace  : /\s+/ .
singleLine  : /\/\/.*/ .
multiLine   : /\/\*(?s).*?\*\// .

identifier  = /\p{ID_Start}\p{ID_Continue}*/ .
literal     = /\"(?:[^\"\\]|\\.)*\"/ .
regex       = /\/(?:[^\/\\]|\\.)*\// .
action      = /@(?:[^@\\]|\\.)*@/ .
message     = /\¶(?:[^\¶\\]|\\.)*/ .

S     = { production } { message } .
              
production  = identifier ( ":" | "=" ) selection "." .
selection   = sequence { "|" sequence } .
sequence    = term { term } .
term        = terminal
            | "[" selection "]"
            | "{" selection "}"
            | "<" selection ">"
            | "(" selection ( ")"
                            | ")?"
                            | ")*"
                            | ")+"
                            ) .
terminal    = identifier | literal | regex | action .
