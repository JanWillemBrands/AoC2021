whitespace  : /\s+/ .
linecomment  : /\/\/.*/ .
blockcomment   : /\/\*(?s).*?\*\// .

identifier  = /\p{XID_Start}\p{XID_Continue}*/ .
literal     = /\"(?:[^\"\\]|\\.)*\"/ .
regex       = /\/(?:[^\/\\]|\\.)*\// .
action      = /@(?:[^@\\]|\\.)*@/ .
message     = /^^^(?:\/(?:[^\/\\]|\\.)*\/|[^^^^\/]*)*/ .

S           = { production } { message } .
              
production  = identifier @ACTION@ ( ":" | "=" ) selection "." .
selection   = sequence { "|" sequence } .
sequence    = term { term } .
term        = terminal
            | "[" selection "]"
            | "{" selection "}"
            | "<" selection ">"
            | "(" selection ( ")"
                            | ")?"
                            | ")*"
                            | ")+"
                            ) .
terminal    = identifier | literal | regex | action .

^^^
whitespace  : /\s+/ .
linecomment  : /\/\/.*/ .
blockcomment   : /\/\*(?s).*?\*\// .

identifier  = /\p{XID_Start}\p{XID_Continue}*/ .
literal     = /\"(?:[^\"\\]|\\.)*\"/ .
regex       = /\/(?:[^\/\\]|\\.)*\// .
action      = /@(?:[^@\\]|\\.)*@/ .
message     = /^^^(?:\/(?:[^\/\\]|\\.)*\/|[^^^^\/]*)*/ .

S           = { production } { message } .
              
production  = identifier @ACTION@ ( ":" | "=" ) selection "." .
selection   = sequence { "|" sequence } .
sequence    = term { term } .
term        = terminal
            | "[" selection "]"
            | "{" selection "}"
            | "<" selection ">"
            | "(" selection ( ")"
                            | ")?"
                            | ")*"
                            | ")+"
                            ) .
terminal    = identifier | literal | regex | action .
