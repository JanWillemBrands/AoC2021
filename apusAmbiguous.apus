whitespace   : /\s+/ .
linecomment  : /\/\/.*/ .
blockcomment : /\/\*(?s).*?\*\// .

identifier  = /\p{XID_Start}\p{XID_Continue}*/ .
literal     = /\"(?:[^\"\\]|\\.)+\"/ .
regex       = /\/(?:[^\/\\]|\\.)+\// .
action      = /@(?:[^@\\]|\\.)+@/ .
message     = /\^\^\^(?:(?s).*?)(?=\^\^\^|$)/ .

grammar     = < production > { message } .
              
production  = rule | silentRule .
rule        = identifier "=" ( regex | selection ) ( "." | ";" ) .
silentRule  = identifier ":" ( regex | literal ) ( "." | ";" ) .
selection   = sequence { "|" sequence } .
sequence    = term { term } .
term        = terminal | option | zeroOrMore | oneOrMore | grouping .
option      = "(" selection ")?" | "[" selection "]" .
zeroOrMore  = "(" selection ")*" | "{" selection "}" .
oneOrMore   = "(" selection ")+" | "<" selection ">" .
grouping    = "(" selection ")" .
terminal    = identifier | literal | regex | action | epsilon .
epsilon     = "" | "#" | /[Î•ÎµÏµÔÔ‘ğ›†ğ›œğœ€ğœ–ğœºğğ´ğŠğ®ğŸ„]/ .

^^^
whitespace   : /\s+/ .
linecomment  : /\/\/.*/ .
blockcomment : /\/\*(?s).*?\*\// .

identifier  = /\p{XID_Start}\p{XID_Continue}*/ .
literal     = /\"(?:[^\"\\]|\\.)+\"/ .
regex       = /\/(?:[^\/\\]|\\.)+\// .
action      = /@(?:[^@\\]|\\.)+@/ .
message     = /\^\^\^(?:(?s).*?)(?=\^\^\^|$)/ .

grammar     = < production > { message } .
              
production  = rule | silentRule .
rule        = identifier "=" ( regex | selection ) ( "." | ";" ) .
silentRule  = identifier ":" ( regex | literal ) ( "." | ";" ) .
selection   = sequence { "|" sequence } .
sequence    = term { term } .
term        = terminal | option | zeroOrMore | oneOrMore | grouping .
option      = "(" selection ")?" | "[" selection "]" .
zeroOrMore  = "(" selection ")*" | "{" selection "}" .
oneOrMore   = "(" selection ")+" | "<" selection ">" .
grouping    = "(" selection ")" .
terminal    = identifier | literal | regex | action | epsilon .
epsilon     = "" | "#" | /[Î•ÎµÏµÔÔ‘ğ›†ğ›œğœ€ğœ–ğœºğğ´ğŠğ®ğŸ„]/ .
