whitespace   : /\s+/ .
linecomment  : /\/\/.*/ .
blockcomment : /\/\*(?s).*?\*\// .

identifier  = /\p{XID_Start}\p{XID_Continue}*/ .
literal     = /\"(?:[^\"\\]|\\.)*\"/ .     // this catches epsilon as ""
epsilon     = /[Î•ÎµÏµÔÔ‘ğ›†ğ›œğœ€ğœ–ğœºğğ´ğŠğ®ğŸ„#]/ .          // include # to satisfy ART
regex       = /\/(?:[^\/\\]|\\.)+\// .
action      = /@(?:[^@\\]|\\.)+@/ .
message     = /\^\^\^(?:(?s).*?)(?=\^\^\^|$)/ .

grammar     = < production > { message } .
              
production  = identifier ( ":" | "=" ) selection "." .
selection   = sequence { "|" sequence } .
sequence    = < term [ "?" | "*" | "+" ] > .
term        = terminal
            | "[" selection "]"
            | "{" selection "}"
            | "<" selection ">"
            | "(" selection ")" .
terminal    = identifier | literal | regex | action | epsilon .

^^^
whitespace   : /\s+/ .
linecomment  : /\/\/.*/ .
blockcomment : /\/\*(?s).*?\*\// .

identifier  = /\p{XID_Start}\p{XID_Continue}*/ .
literal     = /\"(?:[^\"\\]|\\.)*\"/ .     // this catches epsilon as ""
epsilon     = /[Î•ÎµÏµÔÔ‘ğ›†ğ›œğœ€ğœ–ğœºğğ´ğŠğ®ğŸ„#]/ .          // include # to satisfy ART
regex       = /\/(?:[^\/\\]|\\.)+\// .
action      = /@(?:[^@\\]|\\.)+@/ .
message     = /\^\^\^(?:(?s).*?)(?=\^\^\^|$)/ .

grammar     = < production > { message } .
              
production  = identifier ( ":" | "=" ) selection "." .
selection   = sequence { "|" sequence } .
sequence    = < term [ "?" | "*" | "+" ] > .
term        = terminal
            | "[" selection "]"
            | "{" selection "}"
            | "<" selection ">"
            | "(" selection ")" .
terminal    = identifier | literal | regex | action | epsilon .
