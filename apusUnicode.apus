whitespace  : /\s+/ .
singleLine  : /\/\/.*/ .
multiLine   : /\/\*(?s).*?\*\// .

ідентифікатор  = /[\p{L}\p{N}\p{Pc}]+/ .  // identifier
आक्षरिक     = /\"(?:[^\"\\]|\\.)*\"/ .      // literal
正規表現       = /\/(?:[^\/\\]|\\.)*\// .   // regex
action      = /@(?:[^@\\]|\\.)*@/ .         // פעולה
message     = /\^^^(?:[^\^^^\\]|\\.)*/ .        // رسالة

S     = { production } { message } .
              
production  = ідентифікатор ( ":" | "=" ) selection "." .
selection   = sequence { "|" sequence } .
sequence    = term { term } .
term        = terminal
            | "[" selection "]"
            | "{" selection "}"
            | "<" selection ">"
            | "(" selection ( ")"
                            | ")?"
                            | ")*"
                            | ")+"
                            ) .
terminal    = ідентифікатор | आक्षरिक | 正規表現 | action .

^^^
whitespace  : /\s+/ .
singleLine  : /\/\/.*/ .
multiLine   : /\/\*(?s).*?\*\// .

ідентифікатор  = /[\p{L}\p{N}\p{Pc}]+/ .
आक्षरिक     = /\"(?:[^\"\\]|\\.)*\"/ .
正規表現       = /\/(?:[^\/\\]|\\.)*\// .
action      = /@(?:[^@\\]|\\.)*@/ .
message     = /\^^^(?:[^\^^^\\]|\\.)*/ .

S     = { production } { message } .
              
production  = ідентифікатор ( ":" | "=" ) selection "." .
selection   = sequence { "|" sequence } .
sequence    = term { term } .
term        = terminal
            | "[" selection "]"
            | "{" selection "}"
            | "<" selection ">"
            | "(" selection ( ")"
                            | ")?"
                            | ")*"
                            | ")+"
                            ) .
terminal    = ідентифікатор | आक्षरिक | 正規表現 | action .
